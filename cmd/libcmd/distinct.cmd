@REM
@REM distinct.cmd
@REM

@REM ％* から重複を取り除いたモノを LIBCMD_DISTINCT にセットします。


@REM
@REM 準備
@REM

@SET LIBCMD_DISTINCT_ECHO=@
@IF /I "/?" EQU "%1" GOTO SHOW_HELP
@SET LIBCMD_DISTINCT_REST=%*
@IF /I "/echo" EQU "%1" GOTO ECHO_ON
@GOTO BODY

:ECHO_ON
@CALL "%~dp0headtail.cmd" DUMMY%LIBCMD_DISTINCT_REST%
@SET LIBCMD_DISTINCT_REST=%LIBCMD_HEADTAIL_TAIL%
@SET LIBCMD_DISTINCT_ECHO=
@GOTO BODY


@REM
@REM 処理本体
@REM

:BODY
@CALL "%~dp0headtail.cmd" %LIBCMD_DISTINCT_REST%
%LIBCMD_DISTINCT_ECHO%SET LIBCMD_DISTINCT=%LIBCMD_HEADTAIL_STRICT_HEAD%
@SET LIBCMD_DISTINCT_REST=%LIBCMD_HEADTAIL_TAIL%
:LOOP
@CALL "%~dp0headtail.cmd" %LIBCMD_DISTINCT_REST%
@SET LIBCMD_DISTINCT_STRICT_HEAD=%LIBCMD_HEADTAIL_STRICT_HEAD%
@SET LIBCMD_DISTINCT_HEAD=%LIBCMD_HEADTAIL_HEAD%
@SET LIBCMD_DISTINCT_REST=%LIBCMD_HEADTAIL_TAIL%
@IF /I "" EQU "%LIBCMD_DISTINCT_HEAD%" GOTO :EOF
@CALL "%~dp0find.cmd" %LIBCMD_DISTINCT_STRICT_HEAD% %LIBCMD_DISTINCT%
@IF /I "-1" NEQ "%LIBCMD_FIND_INDEX%" GOTO LOOP
%LIBCMD_DISTINCT_ECHO%SET LIBCMD_DISTINCT=%LIBCMD_DISTINCT% %LIBCMD_DISTINCT_STRICT_HEAD%
@GOTO LOOP


@REM
@REM ヘルプ表示
@REM

:SHOW_HELP
@ECHO コマンドライン引数から重複を取り除いて環境変数LIBCMD_DISTINCTにセットします。
@ECHO.
@ECHO %~n0[%~x0] [/echo] [重複を取り除く引数列]
@ECHO.
@ECHO /echo   標準出力にechoしながら実行します。
@ECHO.
@ECHO このコマンドは headtail.cmd と find.cmd に依存しておりこれらのの実行結果が格納される環境変数の内容が上書きされる為、必要に応じてこのコマンドの呼び出し前に別の環境変数に内容をコピーしてください。
@GOTO :EOF
